buildscript {
	repositories {

		jcenter()
		maven {
			name = 'forge'
			url = 'https://files.minecraftforge.net/maven'
		}
	}

	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
	}
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'java'

version =  '0.0.1'
group = 'me.hink.tweaker' // https://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "Tweaker" + "_" + '1.8.9'
description = "Tweaker is a Minecraft Forge mod that provides tons of general enhancements, bug fixes, and core game tweak features."

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
	version = '1.8.9-latest'
	mappings = 'stable_22'
	runDir = 'run'
	useDepAts = false
}

jar {
	manifest {
		attributes 'FMLCorePlugin': 'me.hink.tweaker.asm.TweakerPlugin'
		attributes 'FMLCorePluginContainsMod': 'true'
		attributes 'FMLCorePluginContainsFMLMod': 'true'
		attributes 'FMLAT': 'tweaker_at.cfg'
	}
}

repositories {
	mavenCentral()
}

dependencies {
	testCompile 'junit:junit:4.12'
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

compileJava {
	options.warnings = true
	options.deprecation = true
	options.compilerArgs += ['-Xlint:unchecked']
}

task cleanConfigs(type: Delete) {
	description 'Delete\'s Forge mod configruation for mod from run directory.'

	delete minecraft.runDir + '/' + 'config' + '/' + 'tweaker.cfg'
	followSymlinks = false
}